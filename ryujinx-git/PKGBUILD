# shellcheck disable=SC2034,SC2154,SC2164

_sdkver='8.0.204'

pkgname='ryujinx-git'
pkgver=1.1.1298.r0.ga23d8cb
pkgrel=1
pkgdesc="Experimental Nintendo Switch Emulator written in C#"
arch=('x86_64')
url='https://www.ryujinx.org/'
license=('MIT')
depends=('zlib' 'hicolor-icon-theme' 'libx11' 'fontconfig')
makedepends=('git' 'desktop-file-utils')
provides=("${pkgname/-git/}")
conflicts=("${pkgname/-git/}")
install=ryujinx.install
source=(
  "git+https://github.com/Ryujinx/Ryujinx"
  "https://dotnetcli.azureedge.net/dotnet/Sdk/$_sdkver/dotnet-sdk-$_sdkver-linux-x64.tar.gz"
)
noextract=("dotnet-sdk-$_sdkver-linux-x64.tar.gz")
sha256sums=(
  'SKIP'
  '0ec834dc0f11a994057cd05d84c6250db726457f2fe308091d50543a5285dd15'
)
b2sums=(
  'SKIP'
  '11dcaf7c7ac60695295d930354f3ce0f428a941702629033ea6fb097c30907c4d63353a89fd0e2be6f525125c57c4747193a0be35fe85249088c0bf1ba521fc1'
)
options=(!strip !debug)

pkgver() {
  cd "Ryujinx"

  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  DOTNET_ROOT="$PWD/dotnet"
  export DOTNET_ROOT

  rm -rf "$DOTNET_ROOT"
  mkdir "$DOTNET_ROOT"
  tar zxf dotnet-sdk-$_sdkver-linux-x64.tar.gz -C "$DOTNET_ROOT"
  export PATH=$DOTNET_ROOT:$DOTNET_ROOT/tools:$PATH

  cd "Ryujinx"

  dotnet clean
  dotnet nuget locals all -c
  dotnet publish src/Ryujinx \
    --configuration Release \
    --output ../publish \
    -p:DebugType=embedded \
    -p:ExtraDefineConstants=DISABLE_UPDATER%2CFORCE_EXTERNAL_BASE_DIR \
    -p:Version="$(git describe --tags)" \
    --runtime linux-x64 \
    --self-contained
}

package() {
  mkdir -pv "$pkgdir/opt/ryujinx/"
  mkdir -pv "$pkgdir/usr/bin/"

  install -D "Ryujinx/distribution/linux/Ryujinx.desktop" "$pkgdir/usr/share/applications/ryujinx.desktop"
  install -D "Ryujinx/distribution/misc/Logo.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/ryujinx.svg"

  cp -Rv "publish/"* "$pkgdir/opt/ryujinx/"

  cat <<EOF > "$pkgdir/usr/bin/ryujinx"
#!/usr/bin/env bash

env DOTNET_EnableAlternateStackCheck=1 /opt/ryujinx/Ryujinx
EOF
  chmod -v +x "$pkgdir/usr/bin/ryujinx"


  desktop-file-edit --set-key="Exec" --set-value="nice -n -10 ryujinx %f" "$pkgdir/usr/share/applications/ryujinx.desktop"
  desktop-file-edit --set-key="StartupWMClass" --set-value="Ryujinx" "$pkgdir/usr/share/applications/ryujinx.desktop"
  desktop-file-edit --set-icon="ryujinx" "$pkgdir/usr/share/applications/ryujinx.desktop"
}
