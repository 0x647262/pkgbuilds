# shellcheck disable=SC2034,SC2154,SC2164

_sdkver='9.0.100-rc.1.24452.12'

pkgname='ryujinx-git'
pkgver=1.1.1386.r0.g2f36a66
pkgrel=1
pkgdesc="Experimental Nintendo Switch Emulator written in C#"
arch=('x86_64')
url='https://www.ryujinx.org/'
license=('MIT')
depends=('zlib' 'hicolor-icon-theme' 'libx11' 'fontconfig')
makedepends=('git' 'desktop-file-utils')
provides=("${pkgname/-git/}")
conflicts=("${pkgname/-git/}")
source=(
  "git+https://github.com/Ryujinx/Ryujinx"
  "https://dotnetcli.azureedge.net/dotnet/Sdk/$_sdkver/dotnet-sdk-$_sdkver-linux-x64.tar.gz"
  global.json.patch
)
noextract=("dotnet-sdk-$_sdkver-linux-x64.tar.gz")
sha256sums=('SKIP'
            '6b17f40ac282131acd52cdfe63f37f687d5edbf576b839596ffc6d51a2d9acb8'
            'SKIP')
b2sums=('SKIP'
        '75d2682a2c9543fc79276f93e5ae1f1323f6a6b1d8aa61daff35b288b6424ef803d8a26846f982afe571688e83ebb6df76ab6678708addf422537edcc35aba0b'
        'SKIP')

options=(!strip !debug)

pkgver() {
  cd "Ryujinx"

  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd Ryujinx

  git apply ../global.json.patch
}

build() {
  DOTNET_ROOT="$PWD/dotnet"
  export DOTNET_ROOT

  rm -rf "$DOTNET_ROOT"
  mkdir "$DOTNET_ROOT"
  tar zxf dotnet-sdk-$_sdkver-linux-x64.tar.gz -C "$DOTNET_ROOT"
  export PATH=$DOTNET_ROOT:$DOTNET_ROOT/tools:$PATH

  cd "Ryujinx"

  dotnet clean
  dotnet nuget locals all -c
  dotnet publish src/Ryujinx \
    --configuration Release \
    --output ../publish \
    -p:DebugType=embedded \
    -p:ExtraDefineConstants=DISABLE_UPDATER%2CFORCE_EXTERNAL_BASE_DIR \
    -p:Version="$(git describe --tags)" \
    --runtime linux-x64 \
    --self-contained
}

package() {
  mkdir -pv "$pkgdir/opt/ryujinx/"
  mkdir -pv "$pkgdir/usr/bin/"

  install -D "Ryujinx/distribution/linux/Ryujinx.desktop" "$pkgdir/usr/share/applications/ryujinx.desktop"
  install -D "Ryujinx/distribution/misc/Logo.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/ryujinx.svg"

  cp -Rv "publish/"* "$pkgdir/opt/ryujinx/"

  cat <<EOF > "$pkgdir/usr/bin/ryujinx"
#!/usr/bin/env bash

env DOTNET_EnableAlternateStackCheck=1 /opt/ryujinx/Ryujinx
EOF
  chmod -v +x "$pkgdir/usr/bin/ryujinx"


  desktop-file-edit --set-key="Exec" --set-value="nice -n -10 ryujinx %f" "$pkgdir/usr/share/applications/ryujinx.desktop"
  desktop-file-edit --set-key="StartupWMClass" --set-value="Ryujinx" "$pkgdir/usr/share/applications/ryujinx.desktop"
  desktop-file-edit --set-icon="ryujinx" "$pkgdir/usr/share/applications/ryujinx.desktop"
}
