# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jeremy Newton (Mystro256) <alexjnewt@gmail.com>

pkgbase=dolphin-emu
pkgname=(
  dolphin-emu
  dolphin-emu-tool
)
pkgver=2407.r26.02e1b94149
pkgrel=1
epoch=1
pkgdesc='A Gamecube and Wii emulator'
arch=(x86_64)
url=https://dolphin-emu.org
license=(GPL2)
depends=(
  libevdev
  libpulse
  libxi
  libxrandr
  qt6-base
  qt6-svg
)
makedepends=(
  cmake
  git
  ninja
)
optdepends=('pulseaudio: PulseAudio backend')
options=(!emptydirs !lto)
_commit=02e1b9414909db1644562b3669967feff203460f
source=(
  dolphin-emu::git+https://github.com/dolphin-emu/dolphin.git#commit=${_commit}
  git+https://github.com/arsenm/sanitizers-cmake.git
  git+https://github.com/curl/curl.git
  git+https://github.com/Cyan4973/xxHash.git
  git+https://github.com/epezent/implot.git
  git+https://github.com/fmtlib/fmt
  git+https://github.com/GPUOpen-LibrariesAndSDKs/VulkanMemoryAllocator.git
  git+https://github.com/KhronosGroup/SPIRV-Cross.git
  git+https://github.com/libsdl-org/SDL.git
  git+https://github.com/libusb/hidapi.git
  git+https://github.com/libusb/libusb.git
  git+https://github.com/lsalzman/enet.git
  git+https://github.com/lz4/lz4.git
  git+https://github.com/mgba-emu/mgba.git
  git+https://github.com/mozilla/cubeb.git
  git+https://github.com/randy408/libspng.git
  git+https://github.com/RetroAchievements/rcheevos.git
  git+https://github.com/syoyo/tinygltf.git
  git+https://github.com/zlib-ng/minizip-ng.git
  git+https://github.com/zlib-ng/zlib-ng.git
)

b2sums=(
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
)

prepare() {
  cd dolphin-emu

  local -r submodules=(
    curl/curl
    xxhash
    implot/implot
    fmt/fmt
    VulkanMemoryAllocator
    spirv_cross/SPIRV-Cross
    SDL/SDL
    hidapi/hidapi-src
    libusb/libusb
    enet
    lz4/lz4
    mGBA/mgba
    cubeb/cubeb
    libspng/libspng
    rcheevos/rcheevos
    tinygltf
    minizip-ng
    zlib-ng/zlib-ng
  )

  for _submodule in "${submodules[@]}"; do
    local submodule="Externals/$_submodule"
    git submodule init "$submodule"
    git config submodule."$submodule".url ../"${submodule##*/}"
    git -c protocol.file.allow=always submodule update "$submodule"
  done

  cd Externals/cubeb/cubeb
  git submodule init cmake/sanitizers-cmake
  git config submodule.cmake/sanitizers-cmake.url "$srcdir"/sanitizers-cmake
  git -c protocol.file.allow=always submodule update cmake/sanitizers-cmake
}

pkgver() {
  cd dolphin-emu
  git describe | sed 's/-/.r/; s/-g/./'
}

build() {
  export CFLAGS+=' -I/usr/include/mbedtls2'
  export CXXFLAGS+=' -I/usr/include/mbedtls2'
  export LDFLAGS+=' -L/usr/lib/mbedtls2'
  cmake -S dolphin-emu -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDISTRIBUTOR=archlinux.org \
    -DENABLE_TESTS=OFF \
    -DUSE_SYSTEM_LIBS=OFF \
    -Wno-dev
  cmake --build build
}

package_dolphin-emu() {
  depends+=(
    hicolor-icon-theme
    libevdev
    libxi
    qt6-base
    qt6-svg
  )

  DESTDIR="$pkgdir" cmake --install build
  install -Dm 644 dolphin-emu/Data/51-usb-device.rules -t "$pkgdir"/usr/lib/udev/rules.d/
  rm -rf "$pkgdir"/usr/{bin/dolphin-tool,include,lib/libdiscord-rpc.a}
}

package_dolphin-emu-tool() {
  install -Dm 755 build/Binaries/dolphin-tool -t "$pkgdir"/usr/bin/
}

# vim: ts=2 sw=2 et:
